select disk 0
clean
convert gpt
rem == 1. Windows RE tools partition ===============
create partition primary size=300
format quick fs=ntfs label="WinRE tools"
set id="de94bba4-06d1-4d40-a16a-bfd50179d6ac"
assign letter=T
rem == 2. System partition =========================
create partition efi size=100
rem ** NOTE: For 4KB-per-sector drives, change this 
rem          value to 260 **
format quick fs=fat32 label="System"
assign letter=S
rem == 3. Microsoft Reserved (MSR) partition =======
create partition msr size=128
rem == 4. Windows partition ========================
rem ==    a. Create the Windows partition. ==========
create partition primary
rem ==    b. Create space for the recovery image. ===
shrink minimum=15000
rem ==    c. Prepare the Windows partition. =========
format quick fs=ntfs label="Windows"
assign letter=W
rem === 5. Recovery image partition ================
create partition primary
format quick fs=ntfs label="Recovery image"

assign letter=R
list volume
exit